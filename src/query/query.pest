// See the pest editor https://pest.rs/#editor for an easy way to see how
// inputs are parsed by this grammar

query         =  { SOI ~ expr ~ EOI }
expr = _{ singleVariableFunc | noVariableFunc | infixExpression }

noVariableFunc     =  { funcNames ~ "()" }
singleVariableFunc =  { funcNames ~ "(" ~ expression ~ ")" }
expression         =  { expr }
funcNames          = _{ latest }
latest             =  { "latest" }

infixExpression = { lookup ~ infixFunction ~ lookupValue }
infixFunction   = { ("=" | "!" | "<" | ">"){2} }

lookup   = { lookupId | lookupName | lookupParam }
lookupId = { "id" }
lookupName = { "name" }
lookupParam = { "parameter:" ~ lookupParamName }
lookupParamName = @{ (ASCII_ALPHANUMERIC | "_" )+ }
lookupValue = { string | boolean | number }

number = @{
    ("-" | "+")?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
boolean = { "true" | "TRUE" | "True" | "false" | "FALSE" | "False" }
string = ${ "\"" ~ inner ~ "\"" }
// Contents of string including double quotes
inner = @{ char* }
// Contents of string without double quotes
char       =  {
    !("\"" | "\\") ~ ANY // Matches any character except " and \ which are not supported in querying atm
}
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
